plugins {
	id "fabric-loom" version "1.0-SNAPSHOT"
}

apply plugin: "java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	testmod
}

dependencies {
	minecraft "com.mojang:minecraft:1.19.3"
	mappings "net.fabricmc:yarn:1.19.3+build.2:v2"
	modImplementation "net.fabricmc:fabric-loader:0.14.11"
	testmodImplementation sourceSets.main.output
	testmodCompileOnly sourceSets.main.compileClasspath
	testmodRuntimeOnly sourceSets.main.runtimeClasspath
	modRuntimeOnly("net.fabricmc.fabric-api:fabric-api:0.68.1+1.19.3") {
		exclude module: "fabric-loader"
	}
}

version = "1.1.0"
archivesBaseName = "Matrix"
group = "org.dimdev"

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	inputs.property "version", project.version
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release.set(targetVersion)
	}
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
	archives jar
}
