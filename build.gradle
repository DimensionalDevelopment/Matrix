plugins {
	id "fabric-loom" version "0.6-SNAPSHOT"
	id "org.cadixdev.licenser" version "0.5.0"
}

apply plugin: "java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	testmod
}

dependencies {
	minecraft "com.mojang:minecraft:21w08b"
	mappings "net.fabricmc:yarn:21w08b+build.8:v2"
	modImplementation "net.fabricmc:fabric-loader:0.11.1"
	testmodImplementation sourceSets.main.output
	testmodCompileOnly sourceSets.main.compileClasspath
	testmodRuntimeOnly sourceSets.main.runtimeClasspath
	modRuntime("net.fabricmc.fabric-api:fabric-api:0.31.2+1.17") {
		exclude module: "fabric-loader"
	}
}

version = "1.0.0"
archivesBaseName = "Matrix"
group = "org.dimdev"

license {
	include '**/*.java'
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	inputs.property "version", project.version
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release.set(targetVersion)
	}
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
	archives jar
}
